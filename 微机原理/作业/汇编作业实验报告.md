# 汇编作业实验报告

**姓名**：霍丙南  **学号**：21200340022 ** 班级**：人工智能第二学士学位

## 一、实验环境

本次实验的实验环境为Window11虚拟机操作系统，指令集架构为ARM。其中硬件环境运行在Apple M1 ARM芯片上。

## 二、实现思路

1. 输入一串字符串包含大小写字符和数字（按回车结束），将其中的小写字符转换为大  写字符并输出
	- **问题分析**
		首先输入得到的是一串字符，需要注意循环遍历这串字符，然后检测字符的ASCII码值是否属于小写字符串的范围内，如果是则将其利用特定的算法转换为大写字符，并重新写入到字符串相应位置上。
	- **解决办法**
		核心为字符串大写转小写，通过对比可以发现，大小写字符ASCII码仅有一位之差，所以可以通过按位与操作进行实现。
	- **代码实现**
```asm
; convert to upper case
ToUpperCase proc near 
    ; waitting user inputs
    lea dx, ToUpperCaseTip
    mov ah, 09H
    int 21H
    lea dx, Endl
    int 21H
    call ReadFromInputBuffer
    ; to upper case
    mov al, InputBuffer[01H] ; load read amount
    cbw
    mov cx, ax              
    mov si, 02H              ; load buffer head address
upper:
    mov al, InputBuffer[si]
    cmp al, 61H
    jc  endloop
    cmp al, 7AH
    ja  endloop
    and al, 5FH
    mov InputBuffer[si], al
endloop:
    inc si    
    loop upper 
    
    ; print content  
    inc si
    mov InputBuffer[si], '$' 
    mov ah, 09H
    lea dx, Endl  
    int 21H
    lea dx, InputBuffer
    add dx, 02H
    mov ah, 09H
    int 21H
    lea dx, Endl
    int 21H
    ret
ToUpperCase endp 
```

2. 在字符串中寻找最大值，输出为<源字符串> The maximum is <最大值>
	- 问题分析
		此类问题为寻找最值问题，对于用户的输入，虽然是字符，但在计算机看来认识一串01二进制序列，或者将其看作按字节编码ASCII码，所以字符串寻找最值也就是ASCII值大小的比较
	- 解决办法
		循环遍历整个输入的序列，姑且假设序列第一个元素为最大值，然后遍历过程中如果元素的值大于假设的最大值，则将最大值替换。代码实现中，每一个遍历的元素的保存在寄存器AL中，最大值保存在寄存器DL中，如果AL的值大于DL，则交换两个积存中值。
	- 代码实现
```asm
; find max number
FindMax proc near 
    mov ah, 09H
    lea dx, FindMaxTip
    int 21H
    lea dx, Endl
    int 21H
    call ReadFromInputBuffer
    mov al, InputBuffer[01H]
    cbw
    mov cx, ax
    jcxz fendloop 
    mov si, 02H
    mov bl, InputBuffer[02H]  ; max ----> bl   
    
FindAsciiCodeMax:
    mov al, InputBuffer[si]
    cmp al, bl
    ja storemax
incandloop:
    inc si
    loop FindAsciiCodeMax
    jmp fendloop 
storemax:
    mov bl, al
    jmp incandloop
fendloop:
    mov ah, 09H
    lea dx, FindMaxTip2
    int 21H
    ; store max ---> input buffer
    mov InputBuffer[02H], bl
    mov InputBuffer[03H], '$'
    lea dx, InputBuffer
    add dx, 02H
    int 21H
    lea dx, Endl
    int 21H
    ret
FindMax endp  
```

3. 完成对输入的数组进行排序，用户输入一组十进制数（0～255）
	- 问题分析
		通过分析问题可以将算法分为三个过程：第一为接收用户输入然后将用户输入的ASCII码字符流转换为十六进制数字的过程。第二为对转换之后的数字进行排序的过程。第三为将排序好的数字再转换为ASCII码字符流并重新输出的过程。
	- 解决办法
		ASCII码转十六进制数字，可以定义一个MyAtoI的过程函数进行实现，在程序中所有的输入会保存在预先定义好的缓冲区中，我们只需遍历整个缓冲区就可以并按照分隔符（这里定义为空格，它是两个数字分隔的标志符）分隔ASCII码字符流，并将其转换为十六进制数字。
		
		排序采用冒泡排序算法，将转换好的数据重新排序。
		
		第三个过程将数字再次转换为ASCII码是问题的难点所在，目前并未找到良好的解决思路。
	- 代码实现

```asm
; NumBuffer ---> dx
MyAtoI proc near
    ;;; atoi will use cx, si, ax, bx
    ;;; push it to stack
    push cx
    push si
    push ax
    push bx
    
    ;;; 0 ---> dx
    xor dx, dx
    ;;; NumBuffer size ---> al ---> ax
    mov al, NumBuffer[01H]
    cbw       
    ;;; ax ---> cx
    mov cx, ax
    ;;; ax ---> si
    mov si, ax
    jcxz myatoi_end_loop
    ;;; 1 ---> bl
    mov bl, 01H
myatoi_loop: 
    xor ax, ax
    mov al, NumBuffer[01H][si]
    ;;; al x bl ---> al
    mul bl             
    ;;; dx + ax ---> dx
    add dx, ax
    ;;; bl x 10 ---> bl
    mov al, 0AH
    mul bl
    mov bl, al
    dec si
    loop myatoi_loop
    
     
myatoi_end_loop:
    ;;; NumBuffer size = 0
    mov NumBuffer[01H], 00H
    ;;; pop register 
    pop bx   
    pop ax
    pop si
    pop cx
    ret
MyAtoI endp

               
; InputBuffer ---> NumArrayBuffer
;; number string ===> decimal number 
;; split character = SPACE
;; result is NumArrayBuffer
InitNumArrayBuffer proc near    
    ;;; NumArrayBuffer size ---> 0
    mov NumArrayBuffer[01H], 00H
    ;;; input buffer size ---> al
    mov al, InputBuffer[01H]
    cbw                
    ;;; input buffer size ---> cx
    mov cx, ax
    ;;; if cx == 0?
    jcxz end_init_num_array_buffer
    ;;; add a guard char to the buffer tail
    mov si, ax
    mov InputBuffer[02H][si], ' '
    mov si, 0000H
inab_loop:
    ;;; input buffer [i] ---> al
    mov al, InputBuffer[02H][si]
    ;;; compare al == SPACE ?
    cmp al, 20H
    jz inab_invoke_atoi
    ;;; al ---> num buffer
    xor bx, bx
    ;;;; num buffer size ---> bl
    mov bl, NumBuffer[01H]
    ;;;; al ===> decimal number
    sub al, 30H 
    mov NumBuffer[02H][bx], al
    ;;;; inc num buffer size
    inc bx
    ;;;; bl ----> number buffer size
    mov NumBuffer[01H], bl
inab_inc_and_loop:
    inc si
    loop inab_loop
    jmp end_init_num_array_buffer
    
inab_invoke_atoi:
    call MyAtoI
    ;;; dx ---> NumArrayBuffer
    push bx
    xor bx, bx
    mov bl, NumArrayBuffer[01H]
    mov NumArrayBuffer[02H][bx], dl
    inc bx
    mov NumArrayBuffer[01H], bl
    pop bx
    jmp inab_inc_and_loop     
    
end_init_num_array_buffer:
    ret
InitNumArrayBuffer endp

; buble sort
;; NumArrayBuffer ---> NumArrayBuffer
BubleSort proc near            
    ;;; NumArrayBuffer size ---> cx
    mov al, NumArrayBuffer[01H]
    cbw
    mov cx, ax
    dec cx    
buble_sort_outter:
    push cx 
    mov si, 0000H
buble_sort_inner: 
    mov al, NumArrayBuffer[02H][si]
    mov bl, NumArrayBuffer[02H][si + 1]
    
    cmp al, bl
    ja buble_sort_exchange
    jmp buble_sort_no_exchange 
buble_sort_exchange:
    xchg al, bl
    mov NumArrayBuffer[02H][si], al
    mov NumArrayBuffer[02H][si + 1], bl
buble_sort_no_exchange:
    inc si
    loop buble_sort_inner
    pop cx 
    loop buble_sort_outter

    ret
BubleSort endp   
               
; print NumArrayBuffer
;; NumArrayBuffer ---> InputBuffer
;; InputArray each element + 30H ===> ASCII code
PrintNumBufferArray proc near
    mov al, NumArrayBuffer[01H]
    cbw
    mov cx, ax
    mov si, 0000H
    mov di, 0000H
print_num_buffer_arrray_cpy:
    mov al, NumArrayBuffer[02H][si] 
    rol al, 04H
    ; save 4 low bits
    and al, 0FH 
    ; add 30H
    add al, 30H
    cmp al, 39H
    ja print_num_buffer_array_add 
    jmp print_num_buffer_array_write
print_num_buffer_array_add:
    add al, 07H
print_num_buffer_array_write:
    mov InputBuffer[02H][di], al
print_num_buffer_array_write_space:
    inc di
    mov InputBuffer[02H][di], ' '
end_print_num_buffer_arrray_cpy:
    inc di
    inc si
    loop print_num_buffer_arrray_cpy 
    inc di
    mov InputBuffer[02H][di], '$'   
    mov ah, 09H
    lea dx, InputBuffer
    add dx, 02H
    int 21H
    ret
    
PrintNumBufferArray endp

; sort array
SortArray proc near
    mov ah, 09H
    lea dx, SortArrayTip
    int 21H
    lea dx, Endl
    int 21H
    
    call ReadFromInputBuffer
    call InitNumArrayBuffer 
    call BubleSort
                
    mov ah, 09H
    lea dx, Endl
    int 21H
    
    call PrintNumBufferArray
    
    ret
SortArray endp
```
4. 完成时间的现实，提示用户对时，改变系统定时器
	- 问题分析
	- 解决办法
	- 代码实现

5. 结束程序运行，返回操作系统
	- 问题分析
		此问题是所有问题中最简单的一个，只需调用系统终端即可 。
	- 解决办法
		根据中断向量表的定义，查询到当功能号为00H时，调用21号系统中断会退出系统。
	- 代码实现
```asm
    
InvokeExit:
    mov ah, 00H
    int 21H
    ret
```